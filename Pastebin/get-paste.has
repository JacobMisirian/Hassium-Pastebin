#!/usr/bin/hassium

# We need to import the net module to use htmlEncode() and
# The MySql DLL module to connect to the database.
use Net;
use "MySql.dll";
use "header.has";

func main () {
	try {
		# Display headers
		printHeader();

		# Initialize variables and get our target from the QUERY_STRING
		title = "";
		content = "";
		lang = "";
		hash = cgi.get["hash"];

		# Create our connection
		conn = new Sql("localhost", "Paste", "root", "PASSWORD");
		conn.open();

		# Get the values from the database where the hashes are equal
		data = conn.select("SELECT * FROM pastes WHERE hash=@val1", ["@val1", hash]);

		#Store the values from the database in their respective variables
		while (data.read()) {
			title = data.get("name");
			content = data.get("paste");
			lang = data.get("lang");
		} else {
			println("<h3>The requested paste could not be found in the database.</h3>");
			exit(1);
		}

		# Close the connection
		conn.close();

		# Display the results in an HTML escaped format while replacing
		# newlines with <br>
		println("<b>Title: </b>" + HttpUtility.htmlEncode(title) + "<br>");
		println("<br><b>Language: </b>" + lang + "<br>");
		println("<br><b>Share URL: </b>" + "<div class='ui input' style='margin-left: 5px;'><input type='text' onClick='this.select();' style='width: 600px;' readonly value='http://HassiumLang.com/Paste/get-paste.has?hash=" + hash + "'></input></div><br>");
		println("<br><b>Content:</b><br><br>");
		print("<div id=\"editor\" name=\"content\" style=\"width:100%;height: calc(100% - 260px);\">");
		print(HttpUtility.htmlEncode(content));
	    	println("</div>");
		println("<script src=\"../Hassium/ace/src-noconflict/ace.js\" type=\"text/javascript\" charset=\"utf-8\"></script>
        	<script>
			var temp = false;
			var editor = ace.edit(\"editor\");
			editor.setTheme(\"ace/theme/monokai\");
			editor.getSession().setMode(\"ace/mode/" + lang + "\");
			editor.getSession().setTabSize(4);
			editor.setHighlightActiveLine(true);
			editor.setReadOnly(true);
			editor.resize();
		</script>");

		printFooter();
	} catch {
		print("<script type='text/javascript'>alert('Something has gone wrong');</script>");
	}
}
